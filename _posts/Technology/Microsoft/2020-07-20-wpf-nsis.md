---
title: NSIS and HM NIS Edit
date: 2020-07-20 22:20:32
tags: NSIS HM NIS Edit
---

功能强大的打包工具和NSIS Script Editor  
NSIS (Nullsoft Scriptable Install System) is a professional open source system to create Windows installers  

[NSIS 下载地址](https://nsis.sourceforge.io/Download)

[HM NIS Edit 下载地址](http://hmne.sourceforge.net/)

### 此功能包含

- 欢迎界面
- 注册协议界面
- 组件界面
- 安装目录界面
- 完成界面
- 卸载界面

### 支持

- 压缩比最优化
- 批量文件夹
- 启动外部三方程序
- 安装完成后启动程序
- 卸载面板显示安装空间大小
- 卸载
- 检测系统位数

### 不支持

- 多次安装提示

### 脚本源码

```sh
# save Common.nsh
Unicode true
!ifdef NSIS_INCLUDE_DIR
!addincludedir ${NSIS_INCLUDE_DIR}
!endif

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "4Dogs"
!define PRODUCT_VERSION "1.0"
!define PRODUCT_WEB_SITE "http://www.4dogs.cn/"
!define PRODUCT_PUBLISHER "快乐至上（北京）科技发展有限公司"
!define CONTROL_PANEL_NAME "天象综合渗透测试平台"

!define PROGRAM_NAME "TxMainApp"
!define PROGRAM_UNINSTALLER_NAME "uninstall.exe"
!define PROGRAM_FULL_NAME "The ${PRODUCT_NAME} Penetration Test Platform"
!define PROGRAM_NAME_PATH "${PROGRAM_NAME}.exe"
!define PROGRAM_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\${PROGRAM_NAME}.exe"
!define PROGRAM_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROGRAM_NAME}"
!define PROGRAM_UNINST_ROOT_KEY "HKLM"
; MUI end ------
Name "${CONTROL_PANEL_NAME}"

XPStyle on

!macro IsWiresharkRunning
; See if Wireshark is running
; https://nsis.sourceforge.io/Check_whether_your_application_is_running
${Do}

    System::Call 'kernel32::OpenMutex(i 0x100000, b 0, t "Global\${PROGRAM_NAME}-is-running-{51729CE3-0FCC-4984-A3CA-644FE923600A}") i .R0'
        IntCmp $R0 0 checkRunningSession
        System::Call 'kernel32::CloseHandle(i $R0)'
        Goto isRunning

checkRunningSession:
    System::Call 'kernel32::OpenMutex(i 0x100000, b 0, t "${PROGRAM_NAME}-is-running-{51729CE3-0FCC-4984-A3CA-644FE923600A}") i .R0'
        IntCmp $R0 0 notRunning
        System::Call 'kernel32::CloseHandle(i $R0)'

isRunning:
    ; You'd better go catch it.
    MessageBox MB_RETRYCANCEL|MB_ICONEXCLAMATION "${PROGRAM_NAME} or one of its associated programs is running.$\r$\nPlease close it first." /SD IDCANCEL IDRETRY continueChecking
    Quit

notRunning:
    ${ExitDo}

continueChecking:
${Loop}
!macroend
```

```sh
# save installer.nsh
; Script generated by the HM NIS Edit Script Wizard.
Unicode true
ManifestDPIAware true

; MUI 1.67 compatible ------
!include "MUI2.nsh"
!include "FileFunc.nsh"
!include "Common.nsh"
!include "x64.nsh"

; Best Compression
SetCompress Auto
SetCompressor /SOLID lzma
SetCompressorDictSize 64
SetDatablockOptimize On


; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "..\..\4Dogs\V3.0_TX\Installer\App.ico"
!define MUI_UNICON "..\..\4Dogs\V3.0_TX\Installer\App.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "..\..\4Dogs\V3.0_TX\Installer\SoftwareLicence.txt"
; 组件选择页面
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN
;
!define MUI_FINISHPAGE_RUN_FUNCTION "LaunchLink"
!insertmacro MUI_PAGE_FINISH
;
Function LaunchLink
ExecShell "" "$INSTDIR\${PROGRAM_NAME}.exe"
FunctionEnd

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "SimpChinese"


OutFile "D:\Desktop\TxInstaller.exe"
InstallDir "$PROGRAMFILES\4dogs"
InstallDirRegKey HKLM "${PROGRAM_DIR_REGKEY}" ""
BrandingText "4dogs.cn Installer"
ShowInstDetails show
ShowUnInstDetails show

; Install exe
; Section ""
;   CreateDirectory "$INSTDIR\Installer"
;   SetOutPath "$INSTDIR\Installer"
;   SetOverwrite ifnewer
;   File /r "D:\4Dogs\V3.0_TX\Installer\nmap-7.12-setup.exe"
;   File /r "D:\4Dogs\V3.0_TX\Installer\HASPUserSetup.exe"
;  ExecWait "$INSTDIR\Installer\nmap-7.12-setup.exe"
;  ExecWait "$INSTDIR\Installer\HASPUserSetup.exe"
; SectionEnd


Section "主程序" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File /r "D:\MyShare\20190829\Debug\*"
  
  CreateDirectory "$SMPROGRAMS\${PRODUCT_NAME}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME}\${CONTROL_PANEL_NAME}.lnk" "$INSTDIR\${PROGRAM_NAME}.exe"
  CreateShortCut "$DESKTOP\${CONTROL_PANEL_NAME}.lnk" "$INSTDIR\${PROGRAM_NAME}.exe"
  ; Delete "$INSTDIR\Installer\nmap-7.12-setup.exe"
  ; Delete "$INSTDIR\Installer\HASPUserSetup.exe"CONTROL_PANEL_NAME
  ; RMDir "$INSTDIR\Installer"
SectionEnd


Section -AdditionalIcons
  CreateDirectory "$SMPROGRAMS\${PRODUCT_NAME}"
  CreateShortCut "$SMPROGRAMS\4dogs\Uninstall.lnk" "$INSTDIR\${PROGRAM_UNINSTALLER_NAME}.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\${PROGRAM_UNINSTALLER_NAME}.exe"
  WriteRegStr HKLM "${PROGRAM_DIR_REGKEY}" "" "$INSTDIR\${PROGRAM_NAME}.exe"
  WriteRegStr ${PROGRAM_UNINST_ROOT_KEY} "${PROGRAM_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PROGRAM_UNINST_ROOT_KEY} "${PROGRAM_UNINST_KEY}" "UninstallString" "$INSTDIR\${PROGRAM_UNINSTALLER_NAME}.exe"
  WriteRegStr ${PROGRAM_UNINST_ROOT_KEY} "${PROGRAM_UNINST_KEY}" "DisplayIcon" "$INSTDIR\${PROGRAM_NAME}.exe"
  WriteRegStr ${PROGRAM_UNINST_ROOT_KEY} "${PROGRAM_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PROGRAM_UNINST_ROOT_KEY} "${PROGRAM_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PROGRAM_UNINST_ROOT_KEY} "${PROGRAM_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

Section "-Finally"
; Compute and write the installation directory size
${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
IntFmt $0 "0x%08X" $0
WriteRegDWORD ${PROGRAM_UNINST_ROOT_KEY} "${PROGRAM_UNINST_KEY}" "EstimatedSize" "$0"
SectionEnd

Function .onInit
  ${IfNot} ${RunningX64}
    MessageBox MB_OK "此版本只能运行在64位机器上.$\n请尝试安装32位版本." /SD IDOK
    Abort
  ${EndIf}
FunctionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) 已成功地从你的计算机移除。"
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "你确实要完全移除 $(^Name) ，其及所有的组件？" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\*"
  RMDir /r $INSTDIR
  Delete "$SMPROGRAMS\${CONTROL_PANEL_NAME}\Uninstall.lnk"
  Delete "$DESKTOP\${CONTROL_PANEL_NAME}.lnk"

  RMDir "$SMPROGRAMS\${PRODUCT_NAME}"
  RMDir "$INSTDIR"

  DeleteRegKey ${PROGRAM_UNINST_ROOT_KEY} "${PROGRAM_UNINST_KEY}"
  DeleteRegKey HKLM "${PROGRAM_DIR_REGKEY}"
  SetAutoClose true
SectionEnd
```
