---
title: NSIS and HM NIS Edit
date: 2020-07-20 22:20:32
tags: NSIS HM NIS Edit
---

功能强大的打包工具和NSIS Script Editor  
NSIS (Nullsoft Scriptable Install System) is a professional open source system to create Windows installers  

[NSIS 下载地址](https://nsis.sourceforge.io/Download)

[HM NIS Edit 下载地址](http://hmne.sourceforge.net/)

### 此功能包含

- 欢迎界面
- 注册协议界面
- 组件界面
- 安装目录界面
- 完成界面
- 卸载界面

### 支持

- 压缩比最优化
- 批量文件夹
- 启动外部三方程序
- 安装完成后启动程序
- 卸载面板显示安装空间大小
- 卸载

### 不支持

- 多次安装提示

### 脚本源码

```sh
; Script generated by the HM NIS Edit Script Wizard.
Unicode true
ManifestDPIAware true

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "天象综合渗透测试平台"
!define PRODUCT_VERSION "1.0"
!define PRODUCT_PUBLISHER "网络安全（北京）科技发展有限公司"
!define PRODUCT_WEB_SITE "http://www.4dogs.cn/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\TxMainApp.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI2.nsh"
!include "FileFunc.nsh"

; Best Compression
SetCompress Auto
SetCompressor /SOLID lzma
SetCompressorDictSize 64
SetDatablockOptimize On


; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "..\..\4Dogs\V3.0_TX\Installer\App.ico"
!define MUI_UNICON "..\..\4Dogs\V3.0_TX\Installer\App.ico"


; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "..\..\4Dogs\V3.0_TX\Installer\SoftwareLicence.txt"
; 组件选择页面
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN
;
!define MUI_FINISHPAGE_RUN_FUNCTION "LaunchLink"
!insertmacro MUI_PAGE_FINISH
;
Function LaunchLink
ExecShell "" "$INSTDIR\TxMainApp.exe"
FunctionEnd

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "SimpChinese"

; MUI end ------
Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "TxInstaller.exe"
InstallDir "$PROGRAMFILES\4dogs\Tx"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
BrandingText "4dogs.cn Installer"
ShowInstDetails show
ShowUnInstDetails show

; Install exe
Section ""
  CreateDirectory "$INSTDIR\Installer"
  SetOutPath "$INSTDIR\Installer"
  SetOverwrite ifnewer
  File /r "D:\4Dogs\V3.0_TX\Installer\nmap-7.12-setup.exe"
  File /r "D:\4Dogs\V3.0_TX\Installer\HASPUserSetup.exe"
 ExecWait "$INSTDIR\Installer\nmap-7.12-setup.exe"
 ExecWait "$INSTDIR\Installer\HASPUserSetup.exe"
SectionEnd


Section "主程序" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File /r "D:\MyShare\20190829\Debug\*"
  CreateShortCut "$DESKTOP\天象综合渗透测试平台.lnk" "$INSTDIR\TxMainApp.exe"
  Delete "$INSTDIR\Installer\nmap-7.12-setup.exe"
  Delete "$INSTDIR\Installer\HASPUserSetup.exe"
  RMDir "$INSTDIR\Installer"
SectionEnd



Section -AdditionalIcons
  CreateDirectory "$SMPROGRAMS\4dogs"
  CreateShortCut "$SMPROGRAMS\4dogs\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\TxMainApp.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\TxMainApp.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

Section "-Finally"

; Compute and write the installation directory size
${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
IntFmt $0 "0x%08X" $0
WriteRegDWORD ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "EstimatedSize" "$0"

SectionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) 已成功地从你的计算机移除。"
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "你确实要完全移除 $(^Name) ，其及所有的组件？" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\*"
  
  RMDir /r $INSTDIR

  Delete "$SMPROGRAMS\天象综合渗透测试平台\Uninstall.lnk"
  Delete "$SMPROGRAMS\天象综合渗透测试平台\Website.lnk"
  Delete "$DESKTOP\天象综合渗透测试平台.lnk"

  RMDir "$SMPROGRAMS\天象综合渗透测试平台"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd
```
